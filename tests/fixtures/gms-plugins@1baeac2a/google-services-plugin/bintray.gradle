/**
* Upload artifacts to Bintray and synchronization with Maven
*
* @param bintrayUser (required) bintray user
* @param bintrayKey (required) bintray API key
* @param bintrayOrg (required) bintray organization
* @param bintrayRepo (required) bintray organization
* @param bintrayGPGPass (optional) gpg passphrase to enable autosigning
* @param bintrayPublish (optional) publishing to bintray
* @param mavenUser (optional) Sonatype user
* @param mavenPassword (optional) Sonatype password
*
* To specify parameters from command line use '-P' prefix
* e.g. -PbintrayUser='user'
*/

apply plugin: 'com.jfrog.bintray'


def requiredParams = ["bintrayUser", "bintrayKey", "bintrayOrg", "bintrayRepo"]
for (param in requiredParams) {
    if (!project.hasProperty(param)) {
        throw new GradleException("Missing '-P${param}' required argument")
    }
}

def BINTRAY_DRYRUN = false
def BINTRAY_PUBLISH = project.has('bintrayPublish') ? project.ext.bintrayPublish : false
def MAVEN_SYNC = false
def MAVEN_CLOSE = '0'

def BINTRAY_PROJECT_NAME = "${project.group}.${project.ext.archivesBaseName}"

publishing.publications.pluginMaven(MavenPublication) {

    pom.withXml {
        asNode().appendNode('description', project.ext.pomDesc)
        asNode().appendNode('url', 'http://tools.android.com/')
        asNode().appendNode('name', BINTRAY_PROJECT_NAME)

        Node license = asNode().appendNode('licenses').appendNode('license')
        license.appendNode('name', 'The Apache Software License, Version 2.0')
        license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
        license.appendNode('distribution', 'repo')

        asNode().appendNode('developers')
            .appendNode('developer')
                .appendNode('name', 'The Android Open Source Project')

        Node scm = asNode().appendNode('scm')
        scm.appendNode('connection', 'git://android.googlesource.com/platform/tools/base.git')
        scm.appendNode('url', 'https://android.googlesource.com/platform/tools/base')
    }
}

bintray {

    user = project.ext.bintrayUser
    key = project.ext.bintrayKey
    dryRun = BINTRAY_DRYRUN
    publish = BINTRAY_PUBLISH
    publications = ['pluginMaven']

    pkg {
        repo = project.ext.bintrayRepo
        userOrg = project.ext.bintrayOrg
        name = BINTRAY_PROJECT_NAME
        desc = project.ext.pomDesc
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        vcsUrl = 'git://android.googlesource.com/platform/tools/base.git'
        version {
            name = project.version
            desc = project.ext.pomDesc + ' version ' + project.version
            released  = new java.util.Date()
        }
    }
}

if (project.hasProperty('bintrayGPGPass')) {

    bintray.pkg.version.gpg {
        sign = true
        passphrase = project.ext.bintrayGPGPass
    }
}

if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {

    bintray.pkg.version.mavenCentralSync {
        sync = MAVEN_SYNC
        user = project.ext.mavenUser
        password = project.ext.mavenPassword
        close = MAVEN_CLOSE
    }

    bintrayUpload.doFirst {
        project.logger.quiet('MavenCentral credentials detected')
        if (MAVEN_SYNC) {
            project.logger.quiet('Uploading to MavenCentral')
        } else {
            project.logger.quiet('MavenSync is disabled. '
                + 'Upload to MavenCentral is cancelled')
        }
    }
}
