/*
 * Copyright 2015 Google LLC.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: '../shared_dependencies.gradle'
apply from: '../test_dependencies.gradle'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId names.applicationId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode rootProject.gitCommitCount
        versionName = '1.1.0'
        archivesBaseName = 'plaid'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        resConfig 'en'
        manifestPlaceholders += [
            crashlyticsEnabled: false
        ]
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        release {
            // There's a Dex Splitter issue when enabling DataBinding & proguard in dynamic features
            // The temporary workaround is to disable shrinking.
            // This issue is tracked in: https://issuetracker.google.com/120517460
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            manifestPlaceholders += [crashlyticsEnabled: true]
        }
        debug {
            ext.alwaysUpdateBuildId = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dynamicFeatures = [
        ':about',
        ':designernews',
        ':dribbble',
        ':search'
    ]
}

dependencies {
    implementation project(':core')
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "com.crashlytics.sdk.android:crashlytics:${versions.crashlytics}"
    implementation "com.google.firebase:firebase-core:${versions.firebase}"
    implementation "com.github.bumptech.glide:glide:${versions.glide}"
    implementation "com.github.bumptech.glide:recyclerview-integration:${versions.glide}"

    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
}

// Must be applied after dependencies. See https://stackoverflow.com/a/38018985
apply plugin: 'com.google.gms.google-services'
